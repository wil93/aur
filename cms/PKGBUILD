# Maintainer: William Di Luigi <williamdiluigi@gmail.com>
#
# https://github.com/wil93/aur/tree/master/cms

pkgname=cms
pkgver=1.1.0
pkgrel=1
_commit=3e43eeb
pkgdesc="CMS, or Contest Management System, is a distributed system for running and (to some extent) organizing a programming contest."
arch=('any')
url="http://cms-dev.github.io/"
license=('AGPL3')
depends=('fpc' 'postgresql' 'postgresql-client' 'python2' 'setuptools'
  'python2-tornado' 'python2-psycopg2' 'python2-sqlalchemy' 'python2-psutil'
  'python2-netifaces' 'python2-crypto' 'python2-pytz' 'python2-six'
  'iso-codes' 'shared-mime-info' 'python2-beautifulsoup3' 'python2-mechanize'
  'python2-mock' 'python2-requests' 'python2-werkzeug' 'python2-gevent'
  'python2-coverage')
conflicts=('cms-git')

source=('https://github.com/cms-dev/cms/tarball/v1.1.0'
        'setup.patch')
sha256sums=('a814053fcaa2768be180f241b053d5bb613b268c3ee21a53e1b8499a9ebc938b'
            '64323a67929b219a2a6e6a643128fe8b8b51f55cb517976b0381e7b082af6c11')

prepare() {
  cd "cms-dev-cms-$_commit"
  patch -p1 -i $srcdir/setup.patch
  sed "s|MAKEPKG_INSTALL_ROOT|$pkgdir|g" -i setup.py
}

build() {
  cd "cms-dev-cms-$_commit"
  python2 setup.py build
}

package() {
  cd "cms-dev-cms-$_commit"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
